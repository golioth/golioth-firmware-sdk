# SPDX-License-Identifier: Apache-2.0
#
# Get Golioth Firmware SDK version
#
# Depends on VERSION.txt in root of repository.
# Version must be encoded as "MAJOR.MINOR.PATCH" (eg: 0.16.0).

file(STRINGS ${CMAKE_CURRENT_LIST_DIR}/../../VERSION.txt GOLIOTH_FIRMWARE_SDK_VERSION_LINES)
list(GET GOLIOTH_FIRMWARE_SDK_VERSION_LINES 0 GOLIOTH_SDK_VERSION)
string(REPLACE "." ";" GOLIOTH_VERSION_LIST ${GOLIOTH_SDK_VERSION})

list(GET GOLIOTH_VERSION_LIST 0 GOLIOTH_MAJOR)
list(GET GOLIOTH_VERSION_LIST 1 GOLIOTH_MINOR)
list(GET GOLIOTH_VERSION_LIST 2 GOLIOTH_PATCH)
set(GOLIOTH_TWEAK 0)

math(EXPR VERSION_CODE "(${GOLIOTH_MAJOR} << 16) + (${GOLIOTH_MINOR} << 8)  + (${GOLIOTH_PATCH})")
math(EXPR VERSION_FULL_INT "(${GOLIOTH_MAJOR} << 24) + (${GOLIOTH_MINOR} << 16) + \
    (${GOLIOTH_PATCH} << 8) + (${GOLIOTH_TWEAK})")

math(EXPR VERSION_NUMBER "${VERSION_CODE}"  OUTPUT_FORMAT HEXADECIMAL)
math(EXPR VERSION_FULL   "${VERSION_FULL_INT}"    OUTPUT_FORMAT HEXADECIMAL)

set(GOLIOTH_VERSION_STRING "${GOLIOTH_MAJOR}.${GOLIOTH_MINOR}.${GOLIOTH_PATCH}")

add_custom_command(
  OUTPUT ${PROJECT_BINARY_DIR}/include/generated/golioth_version.h
  COMMAND ${CMAKE_COMMAND} -DZEPHYR_BASE=${ZEPHYR_BASE}
    -DOUT_FILE=${PROJECT_BINARY_DIR}/include/generated/golioth_version.h
    -DVERSION_TYPE=GOLIOTH
    -DVERSION_FILE=${CMAKE_CURRENT_LIST_DIR}/NULL
    -DGOLIOTHVERSION=${VERSION_FULL}
    -DGOLIOTH_VERSION_NUMBER=${VERSION_NUMBER}
    -DGOLIOTH_VERSION_MAJOR=${GOLIOTH_MAJOR}
    -DGOLIOTH_VERSION_MINOR=${GOLIOTH_MINOR}
    -DGOLIOTH_PATCHLEVEL=${GOLIOTH_PATCH}
    -DGOLIOTH_VERSION_TWEAK=${GOLIOTH_TWEAK}
    -DGOLIOTH_VERSION_STRING=${GOLIOTH_VERSION_STRING}
    -DGOLIOTH_VERSION_EXTENDED_STRING="${GOLIOTH_VERSION_STRING}+${GOLIOTH_TWEAK}"
    -DGOLIOTH_VERSION_TWEAK_STRING="${GOLIOTH_VERSION_STRING}+${GOLIOTH_TWEAK}"
    -P ${ZEPHYR_BASE}/cmake/gen_version_h.cmake
  COMMAND_EXPAND_LISTS
)
add_custom_target(golioth_version_h DEPENDS ${PROJECT_BINARY_DIR}/include/generated/golioth_version.h)
add_dependencies(version_h golioth_version_h)
